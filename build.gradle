plugins {
    id 'java'
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '1.0.0-rc-1'
}

group 'io.github.perpk.buildx'
version '1.0.1-SNAPSHOT'

sourceSets {
    functionalTests {
        groovy.srcDir file("/src/functionalTests/groovy")
        compileClasspath += main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

gradlePlugin {
    plugins {
        buildxGradle {
            id = 'io.github.perpk.buildx-gradle-plugin'
            implementationClass = 'io.github.perpk.buildx.BuildxGradlePlugin'
            displayName = 'Buildx Gradle Plugin'
            description = 'This plugin enables building multi-platform images by using docker buildx and buildkit'
        }
    }
    testSourceSets sourceSets.functionalTests
}

pluginBundle {
    website = 'https://github.com/copiepie/buildx-gradle#readme'
    vcsUrl = 'https://github.com/copiepie/buildx-gradle'
    tags = ['docker', 'buildx', 'multiplatform']
}

repositories {
    mavenCentral()
}

publishing {
    repositories {
        maven {
            name = "github"
            url = uri("https://maven.pkg.github.com/copiepie/buildx-gradle")
            credentials {
                username = System.getenv("PKG_USERNAME")
                password = System.getenv("PKG_PASSWORD")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.perpk.buildx'
            artifactId = 'buildx-gradle-plugin'
            version = project.version
        }
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.5.0'
    implementation group: 'commons-collections', name: 'commons-collections', version: '3.2.2'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    testImplementation('org.spockframework:spock-core:2.2-M1-groovy-3.0') {
        exclude module: 'groovy-all'
    }
    functionalTestsImplementation(project)
}

test {
    testLogging {
        exceptionFormat "full"
    }
}

task functionalTests(type: Test) {
    group = 'verification'
    testClassesDirs = sourceSets.functionalTests.output.classesDirs
    classpath = sourceSets.functionalTests.runtimeClasspath
}

tasks.withType(Test) {
    useJUnitPlatform()
}